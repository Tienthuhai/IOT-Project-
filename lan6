#define BLYNK_TEMPLATE_ID   "TMPL6oOC0yUk4"
#define BLYNK_TEMPLATE_NAME "ProjectFinal"
#define BLYNK_AUTH_TOKEN    "dhSbEkuKlVUnsmLLU8GLPZRqZXgKz2KR"

#include <BlynkSimpleEsp32.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include "DHT.h"

// WiFi
char ssid[] = "Thuy Duong";
char pass[] = "0977061097";

// DHT11
#define DHTPIN 23
#define DHTTYPE DHT11
DHT dht(DHTPIN, DHTTYPE);

// LCD
LiquidCrystal_I2C lcd(0x27,16,2);

// Relay pins
#define HEATER_PIN      15
#define FAN_COOL_PIN    5
#define FAN_VENT_PIN    18
#define HUMIDIFIER_PIN  19

// N√∫t nh·∫•n v·∫≠t l√Ω
#define BTN_MODE        13
#define BTN_HEATER      12
#define BTN_HUMIDIFIER  14
#define BTN_FAN_VENT    27
#define BTN_FAN_COOL    26

// Ng∆∞·ª°ng Auto
float TempLow = 37.0;
float TempHigh = 39.0;
float HumLow  = 55.0;
float HumHigh = 60.0;

// Mode: true=AUTO, false=MANUAL
bool autoMode = true;

// Blynk Virtual Pins
#define VP_TEMP_LOW  V7
#define VP_TEMP_HIGH V8
#define VP_HUM_LOW   V9
#define VP_HUM_HIGH  V10
#define VP_MODE      V2
#define VP_HEATER    V3
#define VP_FAN_COOL  V4
#define VP_FAN_VENT  V5
#define VP_HUMIDIFIER V6
unsigned long lastDisplaySwitch = 0;
bool showThreshold = false; // true: hi·ªÉn th·ªã ng∆∞·ª°ng, false: hi·ªÉn th·ªã gi√° tr·ªã th·ª±c

unsigned long lastSend = 0;
const long sendInterval = 2000;

// --- Setup ---
void setup() {
  Serial.begin(115200);
  Blynk.begin(BLYNK_AUTH_TOKEN, ssid, pass);

  dht.begin();
  Wire.begin(22,21);
  lcd.begin(16,2);
  lcd.backlight();

  pinMode(HEATER_PIN, OUTPUT);
  pinMode(FAN_COOL_PIN, OUTPUT);
  pinMode(FAN_VENT_PIN, OUTPUT);
  pinMode(HUMIDIFIER_PIN, OUTPUT);

  pinMode(BTN_MODE, INPUT_PULLUP);
  pinMode(BTN_HEATER, INPUT_PULLUP);
  pinMode(BTN_HUMIDIFIER, INPUT_PULLUP);
  pinMode(BTN_FAN_VENT, INPUT_PULLUP);
  pinMode(BTN_FAN_COOL, INPUT_PULLUP);

  lcd.setCursor(0,0);
  lcd.print("Incubator Ready");
  delay(1500);
  lcd.clear();
}

// --- Nh·∫≠n ng∆∞·ª°ng t·ª´ Blynk ---
// Nhi·ªát ƒë·ªô
BLYNK_WRITE(V7){ 
    float val = param.asFloat();
    if(val < TempHigh) TempLow = val;
    else Blynk.virtualWrite(V7, TempLow);
}

BLYNK_WRITE(V8){ 
    float val = param.asFloat();
    if(val > TempLow) TempHigh = val;
    else Blynk.virtualWrite(V8, TempHigh);
}

// ƒê·ªô ·∫©m
BLYNK_WRITE(V9){ 
    float val = param.asFloat();
    if(val < HumHigh) HumLow = val;
    else Blynk.virtualWrite(V9, HumLow);
}

BLYNK_WRITE(V10){ 
    float val = param.asFloat();
    if(val > HumLow) HumHigh = val;
    else Blynk.virtualWrite(V10, HumHigh);
}

BLYNK_CONNECTED() {
  // Khi ESP32 k·∫øt n·ªëi l·∫°i Blynk (v√≠ d·ª• sau RS), g·ª≠i gi√° tr·ªã m·∫∑c ƒë·ªãnh l√™n cloud
  Blynk.virtualWrite(V7, TempLow);
  Blynk.virtualWrite(V8, TempHigh);
  Blynk.virtualWrite(V9, HumLow);
  Blynk.virtualWrite(V10, HumHigh);
}
// --- Nh·∫≠n Mode t·ª´ Blynk ---
BLYNK_WRITE(V2){
  autoMode = (param.asInt()==0); // 0=Auto,1=Manual
}

// --- Nh·∫≠n relay t·ª´ Blynk Manual ---
BLYNK_WRITE(V3){ if(!autoMode) digitalWrite(HEATER_PIN,param.asInt()); }
BLYNK_WRITE(V4){ if(!autoMode) digitalWrite(FAN_COOL_PIN,param.asInt()); }
BLYNK_WRITE(V5){ if(!autoMode) digitalWrite(FAN_VENT_PIN,param.asInt()); }
BLYNK_WRITE(V6){ if(!autoMode) digitalWrite(HUMIDIFIER_PIN,param.asInt()); }

// --- Auto logic ---
void updateAutoMode(float temp, float hum) {
  if (autoMode) {
    // ---- ƒêi·ªÅu khi·ªÉn theo nhi·ªát ƒë·ªô ----
    if (temp < TempLow) {
      digitalWrite(HEATER_PIN, HIGH);      // B·∫≠t ƒë√®n s∆∞·ªüi
      digitalWrite(FAN_COOL_PIN, LOW);     // T·∫Øt qu·∫°t t·∫£n nhi·ªát
    } 
    else if (temp > TempHigh) {
      digitalWrite(HEATER_PIN, LOW);       // T·∫Øt ƒë√®n s∆∞·ªüi
      digitalWrite(FAN_COOL_PIN, HIGH);    // B·∫≠t qu·∫°t t·∫£n nhi·ªát
    } 
    else {
      digitalWrite(HEATER_PIN, LOW);       // Nhi·ªát ƒë·ªô b√¨nh th∆∞·ªùng ‚Üí t·∫Øt c·∫£ 2
      digitalWrite(FAN_COOL_PIN, LOW);
    }
    // ---- ƒêi·ªÅu khi·ªÉn theo ƒë·ªô ·∫©m ----
    if (hum < HumLow) {
      digitalWrite(HUMIDIFIER_PIN, HIGH);
      digitalWrite(FAN_VENT_PIN, LOW);  // B·∫≠t m√°y phun s∆∞∆°ng
    } 
    else if (hum > HumHigh) {
      digitalWrite(HUMIDIFIER_PIN, LOW);
      digitalWrite(FAN_VENT_PIN, HIGH);   // T·∫Øt m√°y phun s∆∞∆°ng
    }
    else {
      digitalWrite(HUMIDIFIER_PIN, LOW);       // Nhi·ªát ƒë·ªô b√¨nh th∆∞·ªùng ‚Üí t·∫Øt c·∫£ 2
      digitalWrite(FAN_VENT_PIN, LOW);
    }
  }
}

// --- ƒê·ªçc n√∫t nh·∫•n v·∫≠t l√Ω Manual ---
void readPhysicalButtons(){
  if(!autoMode){
    if(digitalRead(BTN_HEATER)==LOW)      digitalWrite(HEATER_PIN, !digitalRead(HEATER_PIN));
    if(digitalRead(BTN_HUMIDIFIER)==LOW)  digitalWrite(HUMIDIFIER_PIN, !digitalRead(HUMIDIFIER_PIN));
    if(digitalRead(BTN_FAN_VENT)==LOW)    digitalWrite(FAN_VENT_PIN, !digitalRead(FAN_VENT_PIN));
    if(digitalRead(BTN_FAN_COOL)==LOW)    digitalWrite(FAN_COOL_PIN, !digitalRead(FAN_COOL_PIN));
    delay(200); // debounce ƒë∆°n gi·∫£n
  }
  // N√∫t chuy·ªÉn Mode
  static bool lastBtnMode = HIGH;
  bool btnState = digitalRead(BTN_MODE);
  if(lastBtnMode==HIGH && btnState==LOW){
    autoMode = !autoMode;
    Blynk.virtualWrite(V2, autoMode?0:1);
  }
  lastBtnMode = btnState;
}

// --- LCD hi·ªÉn th·ªã ---


void displayLCD(float temp, float hum) {
  unsigned long now = millis();

  // C·ª© 10 gi√¢y th√¨ ƒë·∫£o gi·ªØa hi·ªÉn th·ªã ng∆∞·ª°ng v√† gi√° tr·ªã th·ª±c
  if (now - lastDisplaySwitch >= 10000) { // 10s
    lastDisplaySwitch = now;
    showThreshold = !showThreshold;
    lcd.clear();
  }

  if (autoMode && showThreshold) {
    // üî∏ Hi·ªÉn th·ªã ng∆∞·ª°ng (ch·ªâ khi autoMode b·∫≠t)
    lcd.setCursor(0, 0);
    lcd.print("Tg:");
    lcd.print(TempLow, 1);
    lcd.print("-");
    lcd.print(TempHigh, 1);
    lcd.print("C");

    lcd.setCursor(0, 1);
    lcd.print("Hg:");
    lcd.print(HumLow, 0);
    lcd.print("-");
    lcd.print(HumHigh, 0);
    lcd.print("%");
  } else {
    // üîπ Hi·ªÉn th·ªã gi√° tr·ªã th·ª±c t·∫ø
    lcd.setCursor(0, 0);
    lcd.print("T:");
    lcd.print(temp, 1);
    lcd.print("C H:");
    lcd.print(hum, 0);
    lcd.print("%");

    lcd.setCursor(0, 1);
    lcd.print("Mode: ");
    lcd.print(autoMode ? "AUTO  " : "MANUAL");
  }
}


void loop(){
  Blynk.run();
  readPhysicalButtons();

  unsigned long currentMillis = millis();
  if(currentMillis - lastSend >= sendInterval){
    lastSend = currentMillis;

    float temp = dht.readTemperature();
    float hum  = dht.readHumidity();

    if(isnan(temp) || isnan(hum)){
      lcd.setCursor(0,0); lcd.print("Sensor Error   ");
      lcd.setCursor(0,1); lcd.print("Check Device   ");
      return;
    }

    updateAutoMode(temp, hum);
    displayLCD(temp, hum);

    Blynk.virtualWrite(V0,temp);
    Blynk.virtualWrite(V1,hum);
    Blynk.virtualWrite(V3,digitalRead(HEATER_PIN));
    Blynk.virtualWrite(V4,digitalRead(FAN_COOL_PIN));
    Blynk.virtualWrite(V5,digitalRead(FAN_VENT_PIN));
    Blynk.virtualWrite(V6,digitalRead(HUMIDIFIER_PIN));
  }
}
 
